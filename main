[org 0x100]

jmp start

x_snake_s: dw 0
y_snake_s: dw 0
xy_snake_s: dw 0
x_snake_e: dw 0
y_snake_e: dw 0
xy_snake_e: dw 0
last_pos: dw 0
ltmv: dw 1
level: dw 4
move: dw " Last Move: X"
status: dw "ESC-->END"
end_message: dw "Game has been ended"
r_message: 	dw "Press R to Restart   "
score: dw "Score: 000"
xy_fruit: dw 0
x_fruit: dw 0
y_fruit: dw 0


start:
	call select_level
	call bg
	mov ah, 0
	int 0x10
	call game
	jmp end

note:
	push ax
	push bx
	mov al, 182
	out 43h, al
	mov ax, 2711
	out 42h, al
	mov al, ah
	out 42h, al
	in al, 61h
	or al, 00000011b
	out 61h, al
	mov bx, cx
	.pause1:
	mov cx, 65535
	.pause2:
	dec cx
	jne .pause2
	dec bx
	jne .pause1
	in al, 61h
	and al, 11111100b
	out 61h, al
	pop bx
	pop ax
	ret 

select_level:
	ret
	
reset:
	push ax
	push cx
	push di
	push es
	
	mov word[x_snake_s], 25
	mov word[y_snake_s], 2
	mov word[x_snake_e], 13
	mov word[y_snake_e], 2
	
	mov word[ltmv], 1
	mov word[score+7], 48
	mov word[score+8], 48
	mov word[score+9], 48
	mov cx, 4+8
	mov ax, 0xB800
	mov es, ax
	mov ax, word[x_snake_s]
	push ax
	mov ax, word[y_snake_s]
	push ax
	mov ax, xy_snake_s
	push ax
	call get_cords
	mov di, word[xy_snake_s]
	std
	mov ax, 0x03DC
	rep stosw
	
	pop es
	pop di
	pop cx
	pop ax
	ret
game:
	push ax
	push bx
	push cx
	push dx
	push si
	push di
	push es

	call reset
	call Genxy_fruit
	main:
		call check_key
		cmp word[ltmv], 1
		jbe label1
		
		mov ax, [level]
		push ax
		jmp label2
		
		label1:
			mov ax, [level]
			shr ax, 1
			push ax
		label2:
			call delay
			call snake

	jmp main

	pop es
	pop di
	pop si
	pop dx
	pop cx
	pop bx
	pop ax
	ret


check_key:
	mov ah, 0x1
	int 16h
	jz cont1
	mov ah, 0x0
	int 16h
	
	cmp ah, 4Bh
	je left
	cmp ah, 4Dh
	je right
	cmp ah, 48h
	je up
	cmp ah, 50h
	je down
	cmp ah, 01h
	je end
	jmp cont1

left:
	cmp word[ltmv], 1
	je cont1
	add word[x_snake_s], -1
	mov word[ltmv], 0
	mov word[move+12], 'L'
	ret
 
right:
	cmp word[ltmv], 0
	je cont1
	add word[x_snake_s], 1
	mov word[ltmv], 1
	mov word[move+12], 'R'
	ret

up:
	cmp word[ltmv], 3
	je cont1
	add word[y_snake_s], -1
	mov word[ltmv], 2
	mov word[move+12], 'U'
	ret

down:
	cmp word[ltmv], 2
	je cont1
	add word[y_snake_s], 1
	mov word[ltmv], 3
	mov word[move+12], 'D'
	ret

cont1:
	cmp word[ltmv], 0
	je left
	cmp word[ltmv], 1
	je right
	cmp word[ltmv], 2
	je up
	cmp word[ltmv], 3
	je down
	ret

snake:	
	push ax
	push di
	push si
	push es
	mov ax, 0xB800
	mov es, ax
	call info
	
	mov ax, word[x_snake_s]
	push ax
	mov ax, word[y_snake_s]
	push ax
	mov ax, xy_snake_s
	push ax
	call get_cords
	mov di, word[xy_snake_s]
	mov ax, [es:di]
	mov word[last_pos], ax
	
	cmp ax, 0x0CDC
	je cont3
	call update_back_pointer
	jmp cont5
	
	cont3:
	call update_score
	cont5:
	cmp word[ltmv], 1
	jle i1
	mov word[es:di], 0x03B1	
	jmp i2
	i1:
	mov word[es:di], 0x03DC
	i2:
	mov ax, 0x0CDC
	mov di, word[xy_fruit]
	stosw
	
	cmp word[last_pos], 0x0720
	je cont4
	cmp word[last_pos], 0x0CDC
	je cont4
	jmp end
	
	
	cont4:
	pop es
	pop si
	pop di
	pop ax
	ret

update_back_pointer:
	push ax
	push bx
	push cx
	push dx
	push si
	push di
	push es

	mov ax, word[x_snake_e] 
	push ax
	mov ax, word[y_snake_e] 
	push ax
	mov ax, xy_snake_e 
	push ax
	call get_cords
	mov di, word[xy_snake_e]
	
	;right
	mov ax, word [es:di+2]
	cmp ax, 0x03DC
	je q1
	
	;left
	mov ax, word [es:di-2]
	cmp ax, 0x03DC
	je q2
	
	;down
	mov ax, word [es:di+160]
	cmp ax, 0x03B1
	je q3
	
	;up
	mov ax, word [es:di-160]
	cmp ax, 0x03B1
	je q4
	
	jmp cont6
	
	
	q1:
	add word[x_snake_e], 1
	jmp cont6
	q2:
	add word[x_snake_e], -1
	jmp cont6
	q3:
	add word[y_snake_e], 1
	jmp cont6
	q4:
	add word[y_snake_e], -1
	
	cont6:
	mov word[es:di], 0x0720
	
	
	pop es
	pop di
	pop si
	pop dx
	pop cx
	pop bx
	pop ax
	ret
	
update_score:
	push ax
	mov cx, 2
	call note
	mov al, byte[score+9]
	cmp al, '9'
	je label3
	add byte[score+9], 1
	jmp label5
	label3:
	mov al, byte[score+8]
	cmp al, '9'
	je label4
	add byte[score+8], 1
	mov byte[score+9], '0'
	jmp label5
	label4:
	mov cx, 10
	call note
	add byte[score+7], 1
	mov byte[score+8], '0'
	mov byte[score+9], '0'

	label5:
	call Genxy_fruit
	pop ax
	ret 
	
get_cords:
	push bp
	mov bp, sp
	push ax
	push bx

	mov al, [bp+6]
	mov bl, 80
	mul bl
	add ax, [bp+8]
	shl ax, 1
	mov bx, [bp+4]
	mov [bx], ax
	
	pop bx
	pop ax
	pop bp
	ret 6


	
delay:
	push bp
	mov bp, sp
	push cx
	push dx
	push di
	
	mov cx, [bp+4]
	mov di, 0

	dl1: 
		mov dx, 0xFFFF
		dl2: 
			dec dx
			jnz dl2
	loop dl1
	
	pop di
	pop dx
	pop cx
	pop bp
	ret 2


bg:
	push ax
	push cx
	push di
	push es
	
	
	mov ax, 0xB800
	mov es, ax
	mov ah, 0x07
	mov al, 0x20
	mov di, 0
	mov cx, 2000
	cld
	rep stosw
	mov ah, 0x0A
	mov al, 0xB1
	
	mov cx, 24
	mov di, 0
	l3:
		stosw
		add di, 156
		stosw
	loop l3
	
	mov di, 0
	mov cx, 80
	cld
	rep stosw
	mov di, 3520
	mov cx, 80
	cld 
	rep stosw
	
	mov di, 840
	mov cx, 40
	cld 
	rep stosw
	mov di, 1000
	mov cx, 40
	cld 
	rep stosw
	
	mov di, 2440
	mov cx, 40
	cld 
	rep stosw
	mov di, 2600
	mov cx, 40
	cld 
	rep stosw
	call info
	pop es
	pop di
	pop dx
	pop cx
	ret
	
info:
	push bp
	push ax
	push bx
	push cx
	push dx
	push si
	push di
	push es
	
	mov ah, 0x13
	mov al, 1 
	mov bh, 0 
	mov bl, 0xF
	mov dx, 0x1701 
	mov cx, 13
	push cs 
	pop es 
	mov bp, move
	int 0x10 
	
	mov dx, 0x1715
	mov cx, 9
	push cs 
	pop es 
	mov bp, status
	int 0x10 

	mov dx, 0x1725
	mov cx, 10
	push cs 
	pop es 
	mov bp, score
	int 0x10 	
	
	pop es
	pop di
	pop si
	pop dx
	pop cx
	pop bx
	pop ax
	pop bp
	ret
	
Genxy_fruit:

	push cx
	push ax
	push dx
	push es
	push di
	
	mov ax, 0xB800
	mov es, ax
	
	l4:
	call GenRandCoords
	mov ax, word[x_fruit]
	push ax
	mov ax, word[y_fruit]
	push ax
	mov ax, xy_fruit
	push ax
	call get_cords
	mov di, word[xy_fruit]
	mov ax, [es:di]
	cmp ax, 0x0720
	je cont2
	jmp l4
	
	cont2:
	pop di
	pop es
	pop dx
	pop ax
	pop cx
	ret

GenRandCoords:
	push cx
	push ax
	push dx
	
	mov ah, 0x00
	int 0x1A 
	mov ax, dx
	xor dx, dx
	mov cx, 70
	div cx
	
	shr dx, 1
	shl dx, 1
	add dx, 3
	
	mov word[x_fruit],dx
	
	mov ah, 0x00
	int 0x1A 
	mov ax, dx
	xor dx, dx
	mov cx, 20
	div cx
	
	shr dx, 1
	shl dx, 1
	add dx, 2
	
	mov word[y_fruit],dx
	
	pop dx
	pop ax
	pop cx
	ret

ending_message:
	mov ah, 0x13
	mov al, 1 
	mov bh, 0 
	mov bl, 0xEE
	mov dh, 0x0A 
	mov dl, 30
	mov cx, 19
	push cs 
	pop es 
	mov bp, end_message
	int 0x10 
	mov dh, 0x0C 
	mov dl, 30
	push cs 
	pop es 
	mov bp, r_message
	int 0x10 
	mov dh, 0x0D 
	mov dl, 35
	mov cx, 10
	push cs 
	pop es 
	mov bp, score
	int 0x10 
	mov dx, 0x1700
	mov cx, 0
	int 0x10 
	mov ah, 0x0
	
	int 16h
	cmp ah, 19
	je h1
	ret
	h1:
	jmp start
	ret
	
end:
	mov cx, 4
	call note
	call ending_message
mov ax, 0x4C00
int 0x21
